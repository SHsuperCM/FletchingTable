plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version "0.20.0"
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven { url = "https://repo.spongepowered.org/maven/" }
}

version = '1.2'
group = 'io.shcm.shsupercm.fabric'

gradlePlugin {
    // Define the plugin
    plugins {
        fletchingtable {
            id = 'io.shcm.shsupercm.fabric.fletchingtable'
            implementationClass = 'io.shcm.shsupercm.fabric.fletchingtable.FTGradlePlugin'
        }
    }
}

dependencies {
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
    implementation group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
}

task apiJar(type: Jar) {
    archiveName "api.jar"

    from sourceSets.main.output
    from sourceSets.main.java.getSrcDirs()
    include "io/shcm/shsupercm/fabric/fletchingtable/api/**"
}

jar {
    into("META-INF/jars") {
        from apiJar
    }
}

pluginBundle {
    website = 'https://github.com/SHsuperCM/FletchingTable'
    vcsUrl = 'https://github.com/SHsuperCM/FletchingTable'

    description = 'Additions and automations for fabric-loom'

    plugins {
        fletchingtable {
            displayName = "Fletching Table"
            tags = ['fabric', 'fabricmc', 'annotation processor']
        }
    }
}

publishing {
    repositories {
        maven {
            name = 'localPluginRepository'
            url = './local-plugin-repository'
        }
    }
}